data "aws_region" "current" {}

data "aws_availability_zones" "available" {}

locals {
  zone_names = [for s in var.azs : "${data.aws_region.current.name}${s}"]
}

resource "aws_vpc" "main" {
  cidr_block = var.vpc_cidr
  enable_dns_support = true
  enable_dns_hostnames = true
  tags = merge(var.tags, { Name = "iac-vpc" })
}


resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id
  tags = merge(var.tags, { Name = "iac-igw" })
}

resource "aws_subnet" "public" {
  for_each = { for idx, az in local.zone_names : idx => {
    az = az
    cidr = var.public_subnet_cidrs[idx]
  } }
  vpc_id = aws_vpc.main.id
  cidr_block = each.value.cidr
  availability_zone = each.value.az
  map_public_ip_on_launch = true
  tags = merge(var.tags, { Name = "iac-public-${each.key}" })
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id
  tags = merge(var.tags, { Name = "iac-public-rt" })
}

resource "aws_route" "internet" {
  route_table_id = aws_route_table.public.id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id = aws_internet_gateway.igw.id
}

resource "aws_route_table_association" "public" {
  for_each = aws_subnet.public
  subnet_id = each.value.id
  route_table_id = aws_route_table.public.id
}

resource "aws_subnet" "private" {
  for_each = { for idx, az in local.zone_names : idx => {
    az = az
    cidr = var.private_subnet_cidrs[idx]
  } }
  vpc_id = aws_vpc.main.id
  cidr_block = each.value.cidr
  availability_zone = each.value.az
  tags = merge(var.tags, { Name = "iac-private-${each.key}" })
}

resource "aws_route_table" "private" {
  for_each = aws_subnet.private
  vpc_id = aws_vpc.main.id
  tags = merge(var.tags, { Name = "iac-private-rt-${each.key}" })
}


resource "aws_route_table_association" "private" {
  for_each = aws_subnet.private
  subnet_id = each.value.id
  route_table_id = aws_route_table.private[each.key].id
}